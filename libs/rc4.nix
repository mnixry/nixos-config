{
  lib ? (import <nixpkgs> { }).lib,
  ...
}:
let
  N = 255;
  mod = base: mod: base - (mod * (base / mod));
  modN = lib.bitAnd N;
  rangeN = lib.range 0 N;

  stringToBytes =
    str:
    lib.pipe str [
      lib.stringToCharacters
      (map lib.strings.charToInt)
    ];
  ensureBytes =
    bytes:
    if lib.isString bytes then
      stringToBytes bytes
    else if lib.isList bytes then
      bytes
    else
      throw "Unexpected bytes type";

  swap =
    box: ai: bi:
    box
    // lib.optionalAttrs (ai != bi) {
      ${toString ai} = box.${toString bi};
      ${toString bi} = box.${toString ai};
    };

  keySchedule =
    key:
    let
      keyBytes = ensureBytes key;
      keyLength = lib.length keyBytes;
      keyAt = i: lib.elemAt keyBytes (mod i keyLength);
      initial = lib.genAttrs' rangeN (i: lib.nameValuePair (toString i) i);
    in
    lib.foldl'
      (
        { box, j }:
        i:
        let
          j' = modN (j + box.${toString i} + keyAt i);
        in
        {
          box = swap box i j';
          j = j';
        }
      )
      {
        box = initial;
        j = 0;
      }
      rangeN;

  keyStream =
    box: plaintext:
    lib.foldl'
      (
        {
          box,
          ct,
          i,
          j,
        }:
        ptByte:
        let
          i' = modN (i + 1);
          j' = modN (j + box.${toString i'});
          box' = swap box i' j';
          rnd = box'.${toString (modN (box'.${toString i'} + box'.${toString j'}))};
        in
        {
          box = box';
          ct = ct ++ [ (lib.bitXor ptByte rnd) ];
          i = i';
          j = j';
        }
      )
      {
        inherit box;
        ct = [ ];
        i = 0;
        j = 0;
      }
      (ensureBytes plaintext);

  rc4encrypt =
    key: plaintext:
    let
      inherit (keySchedule key) box;
    in
    (keyStream box plaintext).ct;

  # RFC 6229 §2 test vectors — first 32 keystream bytes (XOR against zero plaintext)
  rc4encryptTest = lib.runTests (
    let
      hex = value: (fromTOML "result = ${value}").result;
      zeros = n: lib.genList (_: 0) n;
    in
    {
      testRfc6229_40bit_key1 = {
        expr = rc4encrypt (hex "[0x01, 0x02, 0x03, 0x04, 0x05]") (zeros 32);
        expected = hex "[0xb2, 0x39, 0x63, 0x05, 0xf0, 0x3d, 0xc0, 0x27, 0xcc, 0xc3, 0x52, 0x4a, 0x0a, 0x11, 0x18, 0xa8, 0x69, 0x82, 0x94, 0x4f, 0x18, 0xfc, 0x82, 0xd5, 0x89, 0xc4, 0x03, 0xa4, 0x7a, 0x0d, 0x09, 0x19]";
      };
      testRfc6229_64bit_key1 = {
        expr = rc4encrypt (hex "[0x01, 0x02, 0x03, 0x04, 0x05, 0x06, 0x07, 0x08]") (zeros 32);
        expected = hex "[0x97, 0xab, 0x8a, 0x1b, 0xf0, 0xaf, 0xb9, 0x61, 0x32, 0xf2, 0xf6, 0x72, 0x58, 0xda, 0x15, 0xa8, 0x82, 0x63, 0xef, 0xdb, 0x45, 0xc4, 0xa1, 0x86, 0x84, 0xef, 0x87, 0xe6, 0xb1, 0x9e, 0x5b, 0x09]";
      };
      testRfc6229_128bit_key1 = {
        expr = rc4encrypt (hex "[0x01, 0x02, 0x03, 0x04, 0x05, 0x06, 0x07, 0x08, 0x09, 0x0a, 0x0b, 0x0c, 0x0d, 0x0e, 0x0f, 0x10]") (
          zeros 32
        );
        expected = hex "[0x9a, 0xc7, 0xcc, 0x9a, 0x60, 0x9d, 0x1e, 0xf7, 0xb2, 0x93, 0x28, 0x99, 0xcd, 0xe4, 0x1b, 0x97, 0x52, 0x48, 0xc4, 0x95, 0x90, 0x14, 0x12, 0x6a, 0x6e, 0x8a, 0x84, 0xf1, 0x1d, 0x1a, 0x9e, 0x1c]";
      };
      testRfc6229_256bit_key1 = {
        expr = rc4encrypt (hex "[0x01, 0x02, 0x03, 0x04, 0x05, 0x06, 0x07, 0x08, 0x09, 0x0a, 0x0b, 0x0c, 0x0d, 0x0e, 0x0f, 0x10, 0x11, 0x12, 0x13, 0x14, 0x15, 0x16, 0x17, 0x18, 0x19, 0x1a, 0x1b, 0x1c, 0x1d, 0x1e, 0x1f, 0x20]") (
          zeros 32
        );
        expected = hex "[0xea, 0xa6, 0xbd, 0x25, 0x88, 0x0b, 0xf9, 0x3d, 0x3f, 0x5d, 0x1e, 0x4c, 0xa2, 0x61, 0x1d, 0x91, 0xcf, 0xa4, 0x5c, 0x9f, 0x7e, 0x71, 0x4b, 0x54, 0xbd, 0xfa, 0x80, 0x02, 0x7c, 0xb1, 0x43, 0x80]";
      };
      testRfc6229_128bit_key2 = {
        expr = rc4encrypt (hex "[0xeb, 0xb4, 0x62, 0x27, 0xc6, 0xcc, 0x8b, 0x37, 0x64, 0x19, 0x10, 0x83, 0x32, 0x22, 0x77, 0x2a]") (
          zeros 32
        );
        expected = hex "[0x72, 0x0c, 0x94, 0xb6, 0x3e, 0xdf, 0x44, 0xe1, 0x31, 0xd9, 0x50, 0xca, 0x21, 0x1a, 0x5a, 0x30, 0xc3, 0x66, 0xfd, 0xea, 0xcf, 0x9c, 0xa8, 0x04, 0x36, 0xbe, 0x7c, 0x35, 0x84, 0x24, 0xd2, 0x0b]";
      };
      testRfc6229_256bit_key2 = {
        expr = rc4encrypt (hex "[0x1a, 0xda, 0x31, 0xd5, 0xcf, 0x68, 0x82, 0x21, 0xc1, 0x09, 0x16, 0x39, 0x08, 0xeb, 0xe5, 0x1d, 0xeb, 0xb4, 0x62, 0x27, 0xc6, 0xcc, 0x8b, 0x37, 0x64, 0x19, 0x10, 0x83, 0x32, 0x22, 0x77, 0x2a]") (
          zeros 32
        );
        expected = hex "[0xdd, 0x5b, 0xcb, 0x00, 0x18, 0xe9, 0x22, 0xd4, 0x94, 0x75, 0x9d, 0x7c, 0x39, 0x5d, 0x02, 0xd3, 0xc8, 0x44, 0x6f, 0x8f, 0x77, 0xab, 0xf7, 0x37, 0x68, 0x53, 0x53, 0xeb, 0x89, 0xa1, 0xc9, 0xeb]";
      };
    }
  );
in
builtins.seq
  (lib.assertMsg (
    rc4encryptTest == [ ]
  ) "RC4 encryption tests failed: ${builtins.toJSON rc4encryptTest}")
  {
    inherit rc4encrypt;
  }
